import java.util.concurrent.TimeUnit

buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/' //for Gosu snapshot builds
        }
        jcenter()
    }
    dependencies {
        classpath 'org.gosu-lang.gosu:gradle-gosu-plugin:0.3.1-experimental-SNAPSHOT'
    }
}
apply plugin: 'org.gosu-lang.gosu'

wrapper {
    gradleVersion '3.5'
}


repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/' //for Gosu snapshot builds
    }
    jcenter()
}

dependencies {
    compile 'org.gosu-lang.gosu:gosu-core-api:1-X-SNAPSHOT'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    main {
        java {
            srcDirs = []
        }
        gosu {
            srcDirs += 'src/main/java'
            filter.include '**/*.java'
        }
    }
    test {
        java {
            srcDirs = []
        }
        gosu {
            srcDirs += 'src/test/java'
            filter.include '**/*.java'
        }
    }
    
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    description 'generate javadoc'
    from javadoc
}

task gosudocJar(type: Jar) {
    classifier = 'gosudoc'
    description 'generate gosudoc'
    from gosudoc
}

artifacts {
    archives javadocJar, gosudocJar
}

//don't cache snapshots
configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, TimeUnit.SECONDS
        cacheChangingModulesFor 0, TimeUnit.SECONDS
    }
}
